plugins {
    id "java-gradle-plugin"
    id "org.shipkit.shipkit-changelog"
    id "com.gradle.plugin-publish"
    id "com.adarshr.test-logger"
}

sourceSets {
    test.java.srcDirs = ['src/test/java']
}

configurations {
    fg
    compileOnly.extendsFrom fg
}

repositories {
    maven {
        name = "Fabric Maven"
        url = "https://maven.fabricmc.net"
    }
}

dependencies {
    fg 'net.minecraftforge.gradle:ForgeGradle:5.1.34'
    fg 'net.minecraftforge:srgutils:0.4.3'
    fg 'de.siegmar:fastcsv:2.1.0'

    compileOnly project(":common")
    implementation 'net.fabricmc:mapping-io:0.3.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

jar {
    from project(":common").sourceSets.main.output
    dependsOn project(':common').tasks.jar
}

gradlePlugin {
    plugins {
        tinyForgePlugin {
            id = 'net.galacticraft.gradle.tiny-forge'
            displayName = 'Tiny Forge'
            description = 'On the fly support for tiny mappings in ForgeGradle projects.'
            implementationClass = 'net.galacticraft.plugins.tinyforge.TinyForgePlugin'
        }
    }
}


pluginBundle {
    website = 'https://galacticraft.net/'
    vcsUrl = 'https://github.com/TeamGalacticraft/GalacticGradle'
    tags = ['galacticraft', 'plugin']
}

publishPlugins {
    onlyIf notSnapshot
}