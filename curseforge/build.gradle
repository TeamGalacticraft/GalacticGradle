plugins {
    id 'java-gradle-plugin'
    id "org.shipkit.shipkit-changelog"
    id "com.gradle.plugin-publish"
    id "com.adarshr.test-logger"
}

sourceSets {
	main.resources.srcDirs = ['src/main/resources']
}

configurations {
	provided {
		transitive = false
	}
	compileOnly.extendsFrom(provided)
}

dependencies {
	implementation 'com.squareup.okhttp3:okhttp:3.14.9'
	implementation 'com.squareup.okhttp3:logging-interceptor:3.14.9'
    implementation 'com.google.code.gson:gson:2.9.0'
	implementation 'com.google.guava:guava:31.0.1-jre'
	implementation 'net.sf.trove4j:trove4j:3.0.3'
	implementation gradleTestKit()
	
	provided group: 'net.fabricmc', name: 'fabric-loom', version: '0.7.34'
	
	compileOnly 'org.projectlombok:lombok:1.18.24'
	annotationProcessor 'org.projectlombok:lombok:1.18.24'
	
	testCompileOnly 'org.projectlombok:lombok:1.18.24'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

jar {
	from(configurations.provided.asFileTree.files.collect { zipTree(it) }) {
		exclude '*.xml', '*.json', '*.xml', '*.pom', '*.properties', 'META-INF/**'
	}
}

gradlePlugin {
    plugins {
        curseforgePlugin {
            id = 'net.galacticraft.gradle.publishing.curseforge'
            displayName = 'Galacticraft Curseforge Publishing'
            description = 'Plugin for publishing mod artifacts to the Curseforge Platform'
            implementationClass = 'net.galacticraft.plugins.curseforge.CurseUploadPlugin'
        }
    }
}

pluginBundle {
    website = 'https://galacticraft.net/'
    vcsUrl = 'https://github.com/TeamGalacticraft/GalacticGradle'
    tags = ['galacticraft', 'plugin']
}

publishPlugins {
    onlyIf notSnapshot
}

