plugins {
    id 'java-gradle-plugin'
    id "org.shipkit.shipkit-changelog"
    id "com.gradle.plugin-publish"
    id "com.adarshr.test-logger"
}

configurations {
	provided {
		canBeResolved = true
	}
	api.extendsFrom provided
}

repositories {
	maven {
		url = "https://repo.galacticraft.net/repository/maven-common/"
		content {
			includeGroup 'net.minecraftforge.gradle'
		}
	}
	mavenLocal()
}

dependencies {
	implementation 'net.minecraftforge.gradle:ForgeGradle:5.1.40'
	implementation 'com.google.guava:guava:31.0.1-jre'
	
	provided 'wtf.gofancy.fancygradle:wtf.gofancy.fancygradle.gradle.plugin:1.1.1-6'
	
	compileOnly project(':common')
}

jar {
	from project(":common").sourceSets.main.output

	from(configurations.provided.collect { it.isDirectory() ? it : zipTree(it) }) {
		exclude "META-INF/**"
	}

	dependsOn project(':common').tasks.jar
	
	manifest.from(MANIFEST)
}

gradlePlugin {
    plugins {
        addonPlugin {
            id = 'net.galacticraft.gradle.addon'
			displayName = 'Galacticraft Addon Development'
            description = 'Gradle Plugin for developing Galacticraft Addons'
            implementationClass = 'net.galacticraft.plugins.addon.AddonPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/TeamGalacticraft/GalacticGradle'
    vcsUrl = 'https://github.com/TeamGalacticraft/GalacticGradle'
    tags = ['galacticraft', 'plugin']
}

publishPlugins {
    onlyIf notSnapshot
}
