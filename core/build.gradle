version = "1.0.0-" + getBuildNumber()
group = "net.galacticraft.gradle"

java {
	withSourcesJar()
}

dependencies {
	implementation libs.plexus.utils
	implementation libs.guava
	implementation libs.gson
	implementation libs.commons.text
	implementation libs.checkerframework
	implementation(libs.okhttp) {
		transitive = false
	}
}

jar {
	manifest(setMainManifestProps)
}

publishing {
	publications {
		Core(MavenPublication) {
			artifactId 'core'
            groupId group
            version version

			from components.java
		}
	}
}

String getBuildNumber() {
	def versionFile = file('version.properties')
	if (versionFile.canRead()) {
		Properties versionProps = new Properties()
		versionProps.load(new FileInputStream(versionFile))
		return versionProps['build_num'].toString()
	}
	return "1"
}

task incrementBuild {
	doFirst {
		def versionFile = file('version.properties')
		if (versionFile.canRead()) {
			Properties versionProps = new Properties()
			versionProps.load(new FileInputStream(versionFile))
			def num = versionProps['build_num'].toInteger() + 1
			println("incrementing BuildNumber to: " + num)
			versionProps['build_num'] = num.toString()
			versionProps.store(versionFile.newWriter(), null)
		} else {
			println("version.properties file not found! Ignoring.")
		}
	}
}

tasks.named('publishAllPublicationsToGCGradlePluginsRepository') {
	finalizedBy('incrementBuild')
}
